/*
Level: Boiling Mine
*/

var loadouts = import Custom/loadouts

func equip_melee()
  equipL wand aether
  equipR shield compound

func equip_dps()
  equipL wand aether
  equipR sword vigor

func equip_dashing()
  equipL wand aether
  equipR shield *10 dashing

func equip_ranged_healing()
  equipL wand aether
  equipR ouroboros
  
func equip_ranged_shield()
  equipL wand aether
  equipR shield compound

func equip_armor_piercing()
  equipL hammer poison
  equipR shield compound

?loc = mine
  // Small enemies
  ?foe = master
  ^ | foe = elemental
  ^ | foe = geyser
  ^ | foe = walker
    ?foe.distance > 8
      equip_dashing()
    :?foe.armor > 0
      equip_armor_piercing()
    :
      equip_melee()
  :?hp < 10
    activate potion
  loadouts.healwalk()

?loc = bronze_guardian
  ?time > 600 & foe.distance > 10
    // Guardian is now attackable from afar
    equip_ranged_shield()
  ?pos.y > 0 & foe.distance > 5
    // Guardian hits, we jump on its hand
    equip_dashing()
  :?foe.distance <= 5
    // We are on its arm, close to its face
    ?foe.armor > 0
      equip_armor_piercing()
    :
      equip_dps()
  :?hp < 10
    activate potion
  loadouts.healwalk()