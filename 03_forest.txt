/*
Level: Mushroom Forest
*/

var loadouts = import Custom/loadouts

func equip_melee()
  equipL sword *10 +7
  equipR shield compound

func equip_armor_piercing()
  equipL hammer *8 poison
  equipR long_sword *10 vigor

func equip_dashing()
  equipL sword *10
  equipR shield *10 dashing

func equip_staff()
  equip staff *10 poison

func equip_ranged_healing()
  equipL crossbow *10
  equipR ouroboros

?loc = fungus_forest
  ?hp = maxhp | foe.distance <= 23
    // Healed completely or enemy too close
    equip_staff()
  :
    // Try to heal while attacking from afar
    equip_ranged_healing()
  ?hp <= 10
    activate potion
  loadouts.healwalk()

?loc = fungus_forest_boss  // Boss room
  ?foe = mushroom_boss_skinny  // Enoki
    equip_staff()
  :?foe = mushroom_boss // Angry or Morel
    ?foe.distance >= 6
      // Morel's hand keeps you at distance 5
      equip_dashing()
    :?foe.armor > 0
      equip_armor_piercing()
    :
      equip_melee()
  ?hp <= 10
    activate potion
  loadouts.healwalk()